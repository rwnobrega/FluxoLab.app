Language {
  Expression
    = Expression "||" Disjunct  --binary
    | Disjunct

  Disjunct
    = Disjunct "&&" Conjunct --binary
    | Conjunct

  Conjunct
    = Side ("<=" | "<" | ">=" | ">" | "==" | "!=") Side  --binary
    | Side

  Side
  	= Side ("+" | "-") Term  --binary
  	| Term

  Term
  	= Term ("*" | "/" | div | mod) Factor  --binary
    | Factor

  Factor
    = ("+" | "-" | "!") Factor  --unary
    | Primary

  FunctionCall = identifier "(" ListOf<Expression, ","> ")"

  Parentheses = "(" Expression ")"

  Primary
    = stringLiteral  --stringLiteral
    | numberLiteral  --numberLiteral
    | booleanLiteral  --booleanLiteral
    | FunctionCall
    | constant  --constant
    | identifier  --identifier
    | Parentheses

  read = "read" ~alnum
  write = "write" ~alnum
  true = "true" ~alnum
  false = "false" ~alnum
  div = "div" ~alnum
  mod = "mod" ~alnum

  pi = "pi" ~alnum
  tau = "tau" ~alnum

  keyword = true | false | div | mod | read | write | pi | tau

  identifier = ~keyword (letter | "_") (alnum | "_")*
  constant = pi | tau

  stringLiteral = "\"" character* "\""
  character = "\\\""
            | ~"\"" ~"\n" any

  numberLiteral = digit+ ("." digit+)? (("E" | "e") ("+" | "-")? digit+)?

  booleanLiteral = true | false
}
