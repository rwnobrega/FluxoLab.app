Language {
  Command
    = identifier "=" Expression  --assignment
    | read identifier  --read
    | write NonemptyListOf<Expression, ",">  --write

  Expression
    = Expression "||" Expression0  --binary
    | Expression0

  Expression0
    = Expression0 "&&" Expression1 --binary
    | Expression1

  Expression1
    = Expression2 ("<=" | "<" | ">=" | ">" | "==" | "!=") Expression2  --binary
    | Expression2

  Expression2
  	= Expression2 ("+" | "-") Expression3  --binary
  	| Expression3

  Expression3
  	= Expression3 ("*" | "/" | div | mod) Expression4  --binary
    | Expression4

  Expression4 (uma expressão)
    = ("+" | "-" | "!") Expression4  --unary
    | Primary

  FunctionCall (uma chamada de função)
    = identifier "(" ListOf<Expression, ","> ")"

  Parentheses (uma expressão entre parênteses)
    = "(" Expression ")"

  Primary
    = stringLiteral  --stringLiteral
    | numberLiteral  --numberLiteral
    | booleanLiteral  --booleanLiteral
    | FunctionCall
    | constant  --constant
    | identifier  --identifier
    | Parentheses

  read = "read" ~alnum
  write = "write" ~alnum
  true = "true" ~alnum
  false = "false" ~alnum
  div = "div" ~alnum
  mod = "mod" ~alnum
  pi = "pi" ~alnum
  tau = "tau" ~alnum

  keyword (uma palavra-chave)
    = true | false | div | mod | read | write | pi | tau

  identifier (um identificador)
    = ~keyword (letter | "_") (alnum | "_")*

  constant (uma constante)
    = pi | tau

  stringLiteral (uma string)
    = "\"" ("\\\"" | ~"\"" ~"\n" any)* "\""

  numberLiteral (um número)
    = digit+ ("." digit+)? (("E" | "e") ("+" | "-")? digit+)?

  booleanLiteral (um booleano)
    = true | false
}
